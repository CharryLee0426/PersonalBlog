# <center>微机原理实验报告</center>

| 姓名 | 学号        |
| ---- | ----------- |
| 李晨 | 19030100104 |

## <center>实验一：汇编语言编程实验</center>

### 1. 实验目的

1. 掌握汇编语言的基本语法；
2. 掌握 DOS 系统功能调用的方法；
3. 掌握汇编语言程序的调试运行过程。

### 2. 实验设备

X86 32 位 PC 机 1 台。

### 3. 实验内容

1. 将指定数据区的内容以 ASCII 的形式显示在屏幕上，并通过 DOS 功能调用完成必要的提示信息的展示；
2. 在命令行程序中显示自己的学号姓名信息；
3. 循环从键盘读入字符并回显到屏幕上，然后显示出对应字符的 ASCII 码，直到输入 “Q” 或 “q” 时结束；
4. 自主设计输入显示信息，完成变成与调试，演示实验结果。

实验中使用到的 DOS 功能调用为 `INT 21H`，用到的具体功能调用见下面表格。

| AH   | 功能                               | 调用参数 | 结果               |
| ---- | ---------------------------------- | -------- | ------------------ |
| 1    | 键盘输入并回显                     |          | AL = 输出字符      |
| 2    | 显示单个字符（有 Ctrl+Break 检查） | DL       |                    |
| 6    | 显示单个字符（无 Ctrl+Break 检查） | DL       |                    |
| 8    | 从键盘上读一个字符                 |          | AL = 字符 ASCII 码 |
| 9    | 显示字符串                         | DS:DX    |                    |
| 4CH  | 返回 DOS 系统                      |          | AL = 返回码        |

### 4. 实验步骤

1. 运行星研软件，编写汇编程序；
2. 使用“编译”和“编译连接”命令对写好的汇编程序编译、连接；
3. 找到项目中编译生成的 exe 文件，执行，观察结果；
4. 也可以进入“调试”菜单中点击“进行调试”进入 Debug 模式进行调试观察结果。

### 5. 程序源代码

```assembly
stack segment
	db 128 dup(0)
stack ends

data segment
	nextl db 0ah,0dh,'$'											; 姓名与学号
	msg1 db 'Number:19030100104',0ah,0dh,'$'
	msg2 db 'Name:lichen',0ah,0dh,'$'
	msg3 db 'enter:','$'
	msg4 db 'ASCII:','$'
data ends

code segment
start:	assume ds:data,cs:code,ss:stack
	mov ax,data
	mov ds,ax
	
	;显示学号姓名
	mov dx,offset msg1
	mov ah,9
	int 21h
	
	mov dx,offset msg2
	mov ah,9
	int 21h

	
	;循环输入字符并显示ASCII码
L1:	mov dx,offset msg3
	mov ah,9
	int 21h
	mov ah,1
	int 21h
	mov bh,al
	mov dx,offset nextl
	mov ah,9
	int 21h
	mov dx,offset msg4
	mov ah,9
	int 21h
	
	;转化为ASCII码字符
	mov bl,bh
	shr bl,4
	cmp bl,0ah
	jb  next1
	add bl,7
next1:	add bl,30h
	mov dl,bl
	mov ah,2
	int 21h
	
	mov bl,bh
	and bl,0fh
	cmp bl,0ah
	jb  next2
	add bl,7
next2:	add bl,30h
	mov dl,bl
	mov ah,2
	int 21h
	
	mov dx,offset nextl
	mov ah,9
	int 21h
	
	mov cx,0
	cmp bh,'Q'
	jnz next3
	mov cx,1
next3:  cmp bh,'q'
	jnz next4
	mov cx,1
next4:  jcxz L1

	; Quit
	mov ah,4ch
	int 21h
code ends
end start
```

### 6. 程序运行结果

结果如下：

![](https://pic.imgdb.cn/item/61c861aa2ab3f51d91d0adca.jpg)

结果符合预期，通过验收。

## <center>实验二：数码转换实验</center>

### 1. 实验目的

1. 掌握不同进制数及编码相互转换的程序设计方法；
2. 掌握运算类指令编程和调试方法；
3. 掌握循环程序的设计方法。

### 2. 实验设备

X86 32 位 PC 机 1 台。

### 3. 实验内容

1. 重复从键盘输入不超过5位的十进制数，按回车键结束输入；
2. 将该十进制数转换成二进制数；结果以十六进制数的形式显示在屏幕上；
3. 如果输入非数字字符，则报告出错信息，重新输入；
4. 直到输入 “Q” 或 “q” 时结束；
5. 键盘输入一字符串，以空格结束，统计其中数字字符的个数，并在屏幕显示。

### 4. 数码转换对应关系

| 十六进制 | 二进制 | ASCII 码 |
| -------- | ------ | -------- |
| 0        | 0000   | 30H      |
| 1        | 0001   | 31H      |
| 2        | 0010   | 32H      |
| 3        | 0011   | 33H      |
| 4        | 0100   | 34H      |
| 5        | 0101   | 35H      |
| 6        | 0110   | 36H      |
| 7        | 0111   | 37H      |
| 8        | 1000   | 38H      |
| 9        | 1001   | 39H      |
| A        | 1010   | 41H      |
| B        | 1011   | 42H      |
| C        | 1100   | 43H      |
| D        | 1101   | 44H      |
| E        | 1110   | 45H      |
| F        | 1111   | 46H      |

### 5. 程序源代码

```assembly
DATA SEGMENT
    CTR DB 0DH,0AH,'$'
    ERRORINFO DB  0AH,0DH,"Error! Pls input a NUMBER!$"		; information
    ENDINFO DB  0AH,0DH,"Fin~$"
DATA ENDS
 
CODES SEGMENT
    ASSUME CS:CODES,DS:DATA
START:
    MOV AX,DATA
    MOV DS,AX
 
MAIN:
    CALL READ          ; 读入十进制数, 存放在BX
    CALL NEWLINE
    CALL CONVERT   ; 进行转换并输出
    CALL NEWLINE
    ; CALL EX
    JMP  MAIN               ;跳转到程序结束
    
READ PROC near    ;读入数据
    MOV BX,0
  NEWCHAR:  ;键盘读入新的数字字符
    MOV AH,01H         ; INT 21H, AH=1表示从键盘带回显读入字符到AL中
    INT 21H

    XOR AH,AH
    ; -------------------检测部分
    ;输入换行则结束输入
    CMP AL,13
    JE  EXIT
    ; 输入Q或q结束
    CMP AL,'Q'
    JE EXITQUIT
    CMP AL,'q'
    JE EXITQUIT
    ;如果输入‘0'-'9'之外的字符，则结束读入
    CMP AL,'0'        
    JS  EXITERROR
    CMP AL,'9'
    JA  EXITERROR
    ; -------------------预转换为数字形式
    SUB AL,'0'         
    XCHG BX,AX     
    MOV CX,0AH
    ; 把之前输入的数乘以10，加上本次输入，保存到BX.
    MUL CX            
    XCHG BX,AX
    ADD BX,AX   ; 结果存放BX
    ; 输入下一个数字
    JMP NEWCHAR
   
  EXITERROR: ;报错并重新输入
    MOV DX,OFFSET ERRORINFO ; 报错提示
    MOV AH,09H
    INT 21H
    CALL CRLF
    CALL READ   ; 重新输入
  EXITQUIT:
    CALL EX
  EXIT: ;返回主函数
    RET
READ ENDP
  
CONVERT PROC near   ;转换函数
    MOV CH,04D
  ROTATE:
    MOV CL,04D
    ROL BX,CL        ; BX循环左移4个bit, 也就是把最高位的16进制数放到BL的末尾
    MOV AL,BL
    AND AL,0FH      ; 把4位数放到AL
    ADD AL,30H     ; 转换为ASCII
    CMP AL,'9'
    JBE  PRINT        ;小于9可以直接显示
    ADD AL,07H   ; 大于9转换为'A'-'F'
    
  PRINT:
    MOV DL,AL     ; 字符输出
    MOV AH,2
    INT 21H
    DEC CH
    JNE ROTATE   ; 循环
    RET
CONVERT ENDP
   
NEWLINE PROC near  ; 换行
     LEA DX,CTR
     MOV AH,09H
     INT 21H
     RET
NEWLINE ENDP
   
   
EX PROC
    MOV DX,OFFSET ENDINFO ; 报错提示
    MOV AH,09H
    INT 21H
    MOV AH,4CH
    INT 21H                  ;  程序结束
EX ENDP

CODES ENDS
    END START
```

### 6. 程序运行结果

输入不超过 5 位的十进制数字，会自动转化为 16 进制。比如，输入 123，会输出 7B；输入数字以外的字符则会提示 “Error! Pls input a NUMBER!”；当输入 "q" 或 "Q" 时，会提示 “Fin~”，然后退出程序。程序结果符合预期，顺利通过验收。

## <center>实验三：基本 IO 口拓展实验</center>

### 1. 实验目的

1. 了解 TTL 芯片拓展简单 IO 口的方法；
2. 掌握数据输入输出程序编制的方法。

### 2. 实验设备

X86 32 位 PC 机 1 台。微机原理实验箱 1 台。数据线若干。

### 3. 实验内容

本次实验所用到的引脚图和实验箱总线示意图。

<img src="https://pic.imgdb.cn/item/61c86e082ab3f51d91d4d08f.png" style="zoom:50%;" />

### 4. 实验步骤

1. 实验连线
   * 244 的 CS --- ISA 总线接口模块的 0000H，Y7～Y0 --- 开关 K1～K8
   * 273 的 CS --- ISA 总线接口模块的 0020H，Q7～Q0 --- 发光二极管 L1～L8
   * 该模块的 WR、LD 分别连到 ISA 总线接口模块的 IOWR、IOLD
   * 该模块的数据（AD0～AD7）连到 ISA 总线接口模块的数据（LD0～LD7）
2. 编写实验程序，编译连接，烧入实验箱，运行程序
3. 拨动开关，观察发光二极管的变化。

### 5. 程序源代码

```assembly
IO244		equ	230h		;244(16位)片选
IO273		equ	230h		;273(16位)片选

_stack segment	stack
	       dw 100 DUP(?)	; 堆栈段
_stack		ends

_data segment	word public 'DATA'
_data		ends

code segment
main proc
	        assume cs:code,ds:_data,ss:_stack
	        mov    ax,_data
	        mov    ds,ax
	        mov    bx,0fefeh
	start:  mov    dx,IO244
	        in     al,dx
	        mov    ah,al
	        cmp    al,0ffh                   	;全1
	        jnz    Next0
	        call   ROLeft
	        jmp    Next2
	Next0:  cmp    al,0                      	;全0
	        jnz    Next1
	        call   RORight
	        jmp    Next2
	Next1:  call   Light
	Next2:  call   Delay
	        jmp    start
main endp

ROLeft proc	near
	        rol    bl,1
	        mov    al,bl
	        mov    dx,IO273
	        out    dx,ax
	        ret
ROLeft endp

RORight proc near
	        ror    bl,1
	        mov    al,bl
	        mov    dx,IO273
	        out    dx,ax
	        ret
RORight endp

Light proc near
	        xor    bh,1
	        mov    al,bh
	        mov    dx,IO273
	        out    dx,ax
	        ret
Light endp

Delay proc near                          		;延时
	        xor    cx,cx
	delay1: loop   delay1
	        ret
Delay endp
code ends

END	main
```

### 6. 程序运行结果

首先，在电脑上编译链接通过，然后通过数据线将程序写入实验箱中，波动开关观察结果。开关为低电平时灯亮，高电平时灯灭，当开关全为高电平时，灯从左到右依次点亮，当开关全为低电平时，灯从右往左一次点亮，符合实验预期。并顺利通过验收。

## <center>实验四：可编程并行接口实验</center>

### 1. 实验目的

1. 了解可编程并行接口 8255 的内部结构；
2. 掌握 8255 的工作方式，初始化编程及应用。

### 2. 实验内容

1. A 口读取开关状态，B 口使对应的二极管发光；
1. 开关为高电平时，二极管灭，低电平时，二极管亮；
1. 开关全低时，从左到右亮，全高时，从右到左亮；
1. 自主设计控制及显示模式。

### 3. 实验说明

由于疫情期间，无法前往实验室做实验，只能简要描述实验原理和基本思路。

8255 是一个通用可编程并行接口电路。它具有A、B、C三个8位并行口。其中C口也可用作A、B口的联络信号及中断申请信号。通过编程，它可以被设置为基本输入输出、选通输入输出以及双向传送方式。对于C口还具有按位置0、1的功能。电路图如下所示。

<img src="https://pic.imgdb.cn/item/61c874e42ab3f51d91d6e924.png" style="zoom:50%;" />

8255 A 的三种工作方式

* 0：基本输入输出方式
* 1：选通输入输出方式
* 2：双向选通输入输出方式

### 4. 实验步骤（思路）

对于实验，首先画出实验期间连线框图，框图如下：

![](https://pic.imgdb.cn/item/61c875d12ab3f51d91d745ca.png)

然后按照逻辑编写汇编程序，在电脑上进行编译、链接，由于这里没有实验箱，星研实验环境在 64 位现代电脑上安装较为复杂，所以这里只能验证程序能够正确编译。

由第三次实验可知，实验箱上二极管为共阳极接法，对于流水灯的实现，我们可以使用循环右移和循环左移实现。每次实现前先检验是否满足全高或全低。

通过 AL 寄存器写入 8255 传入数据或设置状态。

```assembly
MOV AL, 90H
MOV DX, 0273H
OUT DX, AL
```

通过 IN 命令读取 8255 中的数据。

```assembly
MOV DX, PA_8255
IN DX, AL	; 将 AL 内容写入 DX 地址处设备中
```

最后，在实验箱上验证结果是否正确。

### 5. 实验源代码

```assembly
COM_8255 EQU 0273H	; 定义接口地址
PA_8255 EQU 0270H
PB_8255 EQU 0271H
PC_8255 EQU 0272H

_STACK SEGMENT STACK
	DW 100 DUP(?)
_STACK ENDS

_DATA SEGMENT WORD PUBLIC 'DATA'
	DB 100 DUP(?)
_DATA ENDS

CODE SEGEMENT
START PROC FAR
	ASSUME CS:CODE, DS:_DATA, SS:_STACK
	MOV AX, _DATA
	MOV DS, AX
	MOV AL, 90H
	MOV DX, COM_8255
	OUT DX, AL
INIT:
	MOV DX, PA_8255		; 初始化 8255 状态
	IN AL, DX
	CMP AL, 0FFH
	JE R2L
	CMP AL, 00H
	JE L2R
	JMP SINGLE
L2R:								; 从左到右流水灯
	CALL L_TO_R
	JMP DelaY
R2L:
	CALL R_TO_L
	JMP DelaY
SINGLE:							; 单个点亮 LED 灯
	MOV DX, PB_8255
	OUT DX, AL
DelaY:							; 延时部分
	CALL DELAY
	JMP INIT
START ENDP

DELAY PROC NEAR		; 延时函数
DELAY1:
	XOR CX, CX
	LOOP $
	RET
DELAY ENDP
L_TO_R PROC NEAR	; 左到右流水灯函数
	MOV AL, 07FH
L_TO_R1:
	PUSH AL
	CALL TEST_ALL:
	CMP AX, 0
	JE L_TO_R_RET1
	POP AL
	MOV DX, PB_8255
	OUT DX, AL
	CALL DELAY
	TEST AL, 1
	JZ L_TO_R_RET
	ROR AL, 1
	JMP L_TO_R1
L_TO_R_RET1:
	POP AL
L_TO_R_RET:
	RET
L_TO_R: ENDP
R_TO_L PROC NEAR	; 右到左流水灯函数，与左到右相似
	MOV AL, 0FEH
R_TO_L1:
	PUSH AL
	CALL TEST_ALL
	CMP AX, 0
	JE R_TO_L_RET1
	POP AL
	MOV DX, PB_8255
	OUT DX, AL
	CALL DELAY
	TEST AL, 080H
	JZ R_TO_L_RET
	ROR AL, 1
	JMP R_TO_L1
R_TO_L_RET1:
	POP AL
R_TO_L_RET:
	RET
R_TO_L ENDP

TEST_ALL PROC NEAR		; 测试函数
	MOV DX, PA_8255
	IN AL, DX
	CMP AL, 0FFH
	JE TEST_ALL_SUCCESS
	CMP AL, 00H
	JE TEST_ALL_SUCCESS
	JMP TEST_ALL_FAILED
TEST_ALL_SUCCESS:
	MOV AX, 1
	RET
TEST_ALL_FAILED:
	MOV AX, 0
	RET
TEST_ALL ENDP
```

### 6. 实验结果

当开关全为低电平时，从左往右亮，当开关全为高电平时，从右往左亮；否则，开关为低电平对应位亮。



### 总结：

通过四次微机原理实验，巩固了课堂上学习的知识，并且学会了结合不同硬件器件编写汇编程序。这些实验是理论与实践的结合，让我对微机原理和汇编语言有了更深刻的认识。

由于最后一次实验遇到了新冠疫情，无法在线下完成实验，也算是这次微机原理实验的一个遗憾。

感谢老师的辛勤付出。